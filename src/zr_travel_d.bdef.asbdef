managed; // implementation in class zbp_r_travel_d unique;
strict ( 2 );
with draft;

define behavior for zr_travel_d //alias <alias_name>
implementation in class zbp_travel_d unique
persistent table /dmo/a_travel_d
draft table zdr_travel_d
lock master
total etag LastChangedAt
etag master LocalLastChangedAt
authorization master ( instance, global )
//etag master <field_name>
{
  create ( precheck );
  update ( precheck );
  delete;
  association _Booking { create ( authorization : update ); with draft; }
  field ( readonly, numbering : managed ) TravelUUID;
  field ( readonly ) TravelID, OverallStatus, TotalPrice, LastChangedAt, LocalCreatedAt, LocalCreatedBy, LocalLastChangedAt, LocalLastChangedBy ;
  field ( mandatory ) CustomerID;

  action ( authorization : update ) acceptTravel result [1] $self;
  action ( authorization : update ) rejectTravel result [1] $self;
  action ( authorization : update ) deductDiscount parameter Za_Deduct_Discount result [1] $self;
  internal action recalcTotalPrice;

  determination setTravelId on save { create; }
  determination setStatusOpen on modify { create; }
  determination calcTotPrice on modify { create; field BookingFee, CurrencyCode; }

  validation validateCustomer on save { create; field CustomerID; }

  side effects { field BookingFee affects field TotalPrice; }

  draft action Resume with additional implementation;
  draft action Edit with additional implementation;
  draft action Activate optimized with additional implementation;
  draft action Discard with additional implementation;

  draft determine action Prepare {
  validation validateCustomer;
  validation zr_booking_d~validateCustomer; }

  mapping for /dmo/a_travel_d
    {
      TravelUUID         = travel_uuid;
      TravelID           = travel_id;
      AgencyID           = agency_id;
      CustomerID         = customer_id;
      BeginDate          = begin_date;
      EndDate            = end_date;
      BookingFee         = booking_fee;
      TotalPrice         = total_price;
      CurrencyCode       = currency_code;
      Description        = description;
      OverallStatus      = overall_status;
      LocalCreatedBy     = local_created_by;
      LocalCreatedAt     = local_created_at;
      LocalLastChangedBy = local_last_changed_by;
      LocalLastChangedAt = local_last_changed_at;
      LastChangedAt      = last_changed_at;
    }
}

define behavior for zr_booking_d //alias <alias_name>
implementation in class zbp_booking_d unique
persistent table /dmo/a_booking_d
draft table zdr_booking_d
lock dependent by _Travel
authorization dependent by _Travel
etag master LocalLastChangedAt
{
  update;
  delete;
  field ( readonly ) TravelUUID, LocalLastChangedAt;
  field ( readonly, numbering : managed ) BookingUUID;
  field ( readonly ) BookingID, BookingDate;
  field ( mandatory ) CustomerID;
  association _Travel { with draft; }
  association _Bookingsuppliment { create ( authorization : update ); with draft; }

  determination setBookingID on save { create; }
  determination setBookingDate on save { create; }
  determination calculateTotalPrice on modify { create; field FlightPrice, CurrencyCode; }

  validation validateCustomer on save { create; field CustomerID; }

  side effects { field FlightPrice affects field _Travel.TotalPrice; }
  mapping for /dmo/a_booking_d
    {
      BookingUUID        = booking_uuid;
      TravelUUID         = parent_uuid;
      BookingID          = booking_id;
      BookingDate        = booking_date;
      CustomerID         = customer_id;
      AirlineID          = carrier_id;
      ConnectionID       = connection_id;
      FlightDate         = flight_date;
      FlightPrice        = flight_price;
      CurrencyCode       = currency_code;
      BookingStatus      = booking_status;
      LocalLastChangedAt = local_last_changed_at;
    }
}

define behavior for zr_bookingsuppliment_D //alias <alias_name>
implementation in class zbp_booking_supp_d unique
persistent table /dmo/a_bksuppl_d
draft table zdr_booksup_d
lock dependent by _Travel
authorization dependent by _Travel
etag master LocalLastChangedAt
//etag master <field_name>
{
  update;
  delete;
  field ( readonly ) BookingUUID, TravelUUID, BookingSupplementId, LocalLastChangedAt;
  field ( readonly, numbering : managed ) BooksupplUuid;

  determination setBookingsupNum on save { create; }
  determination calcBooksupPrice on modify { create; field BookSupplPrice; }

  side effects { field BookingSupplementId affects field _Travel.TotalPrice; }

  association _Travel{ with draft ;}
  association _Booking{ with draft ;}

  mapping for /dmo/a_bksuppl_d
    {
      BooksupplUuid       = booksuppl_uuid;
      TravelUUID          = root_uuid;
      BookingUUID         = parent_uuid;
      BookingSupplementId = booking_supplement_id;
      SupplementId        = supplement_id;
      BookSupplPrice      = price;
      CurrencyCode        = currency_code;
      LocalLastChangedAt  = local_last_changed_at;
    }

}